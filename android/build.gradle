import java.nio.file.Paths

buildscript {
  ext.safeExtGet = {prop, fallback ->
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
  }
  repositories {
    google()
    gradlePluginPortal()
  }
  dependencies {
    classpath("com.android.tools.build:gradle:7.3.1")
    classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:1.7.22")
  }
}

// Based on https://github.com/software-mansion/react-native-reanimated/blob/23578ac8ae134f93c4f59997eac5ebd980a0ce88/packages/react-native-reanimated/android/build.gradle#L23C1-L38C2
def REACT_NATIVE_DIR = {
    def appProject = rootProject.allprojects.find { it.plugins.hasPlugin('com.android.application') }

    def reactNativeLocation = appProject?.ext?.has("REACT_NATIVE_NODE_MODULES_DIR") ? appProject.ext.get("REACT_NATIVE_NODE_MODULES_DIR") : null
    if (reactNativeLocation != null) {
        return file(reactNativeLocation)
    }

    // Fallback to node resolver for custom directory structures like monorepos.
    def reactNativePackage = file(["node", "--print", "require.resolve('react-native/package.json')"].execute(null, rootDir).text.trim())
    if (reactNativePackage.exists()) {
        return reactNativePackage.parentFile
    }

    throw new GradleException(
        "Unable to resolve react-native location in node_modules. You should set a project extension property (in `app/build.gradle`) named `REACT_NATIVE_NODE_MODULES_DIR` with the path to react-native in node_modules."
    )
}.call()

def ANDROID_NDK_HOME = {
    if (rootProject.hasProperty("ndkPath")) {
        return rootProject.ext.ndkPath
    }

    def androidHome = System.getenv("ANDROID_HOME")
    if (androidHome != null && rootProject.hasProperty("ndkVersion")) {
        return Paths.get(androidHome, "ndk", rootProject.ext.ndkVersion)
    }

    throw new GradleException(
        "Unable to resolve ndk path. You should either set a project extension property (in `build.gradle`) named ndkPath or ndkVersion and set ANDROID_HOME env variable."
    )
}.call()

def MIN_SDK_VERSION = {
    if (rootProject.hasProperty("minSdkVersion")) {
        return rootProject.ext.minSdkVersion
    }

    throw new GradleException(
        "Unable to resolve compile sdk version. You should set a project extension property (in `build.gradle`) named minSdkVersion."
    )
}.call()

def LIBSODIUM_SRC = Paths.get(buildscript.sourceFile.getParentFile().getParent(), "libsodium")

def BUILD_LIBSODIUM_AAR = {
    println "libsodium AAR building..."

    if (fileTree(dir:LIBSODIUM_SRC, include:'libsodium-*.aar').size()) {
        println "libsodium AAR building skipped (aar file already exists)"
        return true
    }

    def libsodiumAAR = "${System.getenv('SHELL')} ./dist-build/android-aar.sh".execute(["ANDROID_NDK_HOME=${ANDROID_NDK_HOME}", "NDK_PLATFORM=android-${MIN_SDK_VERSION}"], file(LIBSODIUM_SRC))
    def libsodiumAAROut = new StringBuffer()
    def libsodiumAARErr = new StringBuffer()

    libsodiumAAR.waitForProcessOutput(libsodiumAAROut, libsodiumAARErr)

    if (libsodiumAAR.exitValue() != 0) {
        throw new GradleException(
            "libsodium AAR building failed.\nstdout:\n${libsodiumAAROut}\nstderr:\n${libsodiumAARErr}"
        )
    }

    println "libsodium AAR building completed!"

    return true
}.call()

apply plugin: 'com.android.library'
apply plugin: 'com.facebook.react'
apply plugin: 'org.jetbrains.kotlin.android'

android {
  compileSdkVersion safeExtGet('compileSdkVersion', 33)
  namespace "com.s77rt.rtnsodium"

  buildFeatures {
    prefab true
  }

  defaultConfig {
    minSdkVersion safeExtGet('minSdkVersion', 21)
  
    externalNativeBuild {
      cmake {
        arguments "-DANDROID_STL=c++_shared", "-DREACT_NATIVE_DIR=${REACT_NATIVE_DIR}"
      }
    }
  }

  externalNativeBuild {
    cmake {
      path "src/main/jni/CMakeLists.txt"
    }
  }
}

repositories {
  mavenCentral()
  google()
}

dependencies {
  implementation 'com.facebook.react:react-native'
  implementation fileTree(dir:LIBSODIUM_SRC, include:'libsodium-*.aar')
}
